* CMOS inverter delay characterization - SKY130

.param WN  = 0.42u
.param WP  = 0.84u
.param LCH = 0.15u
.param VDD = 1.8

* Chemin à adapter à TON install ciel
.lib "/home/karim/.local/share/ciel/sky130/versions/<HASH>/sky130A/libs.tech/ngspice/sky130.lib.spice" tt

* ------------------------------------------------------------------
* Subcircuit de l’inverseur standard cell
* ------------------------------------------------------------------
.subckt inv a y VP VN
* d   g  s  b   model                      L       W      nf
XM1 y   a  VN VN  sky130_fd_pr__nfet_01v8  L={LCH} W={WN} nf=1
XM2 y   a  VP VP  sky130_fd_pr__pfet_01v8  L={LCH} W={WP} nf=1
.ends inv

* ------------------------------------------------------------------
* Testbench : entrée PULSE, sortie chargée
* ------------------------------------------------------------------
VDD VP 0 {VDD}
VIN IN 0 PULSE(0 {VDD} 0.1n 50p 50p 1n 2n)

Cload OUT 0 2f

XINV IN OUT VP 0 inv

.tran 1p 5n

* Mesures des délais
.meas tran tpHL TRIG v(IN)  VAL='{VDD/2}' RISE=1 TARG v(OUT) VAL='{VDD/2}' FALL=1
.meas tran tpLH TRIG v(IN)  VAL='{VDD/2}' FALL=1 TARG v(OUT) VAL='{VDD/2}' RISE=1
.meas tran tpavg PARAM='(tpHL + tpLH)/2'

.end
